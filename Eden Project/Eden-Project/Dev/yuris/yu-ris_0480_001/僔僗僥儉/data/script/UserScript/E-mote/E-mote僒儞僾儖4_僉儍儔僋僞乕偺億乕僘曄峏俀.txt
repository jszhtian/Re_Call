//==================================================================
//
// E-moteサンプル [ キャラクターのポーズ変更２ ]
//
//==================================================================
#=EMOTE_キャラクターのポーズ変更２



\FOUT(0)            //フェードアウト(0msかけて幕を下ろす)
\BG(bg_040, 150, 0) //背景をZ=150の位置に0msかけて表示
\FIN(600, 23)       //フェードイン(600msかけて、トランジション定義番号23を使って幕を上げる)


\SP.ST(emote_test, 500)    //立ち絵としてE-moteファイル「emote_test.psb」を読込、500msかけて表示
\SP.ZBXY(50,50,0)          //※定義ファイル行き.
\SP.ZXYZ(0, 150, 200, 0)   //XYZ=(0,150,200)に設置.(正確には(0,0,0)の位置から(0,150,200)の位置へ0msかけて移動をおこなっている)
\SP.GO(CH_1)               //以上の演出を開始


今度は、E-moteエディタであらかじめ作成されたモーションを再生させてみます。


それでは、あらかじめ作られた「sample_01」という名前のタイムラインを再生してみます。


\SP.EM.PLAY(sample_01)     //タイムライン「sample_01」を再生する
\SP.GO(CH_1)               //以上の演出を開始


再生開始しました。


\SP.EM.STOP(sample_01)     //タイムライン「sample_01」の再生を停止する
\SP.GO(CH_1)               //以上の演出を開始


再生を停止しました。もしタイムラインの途中で停止した場合、その時点でのポーズ・表情が維持されます。

もう一度、今度はタイムライン「sample_02」を再生してみます。


\SP.EM.PLAY(sample_02)     //タイムライン「sample_02」を再生する
\SP.GO(CH_1)               //以上の演出を開始


再生しました。


\SP.EM.STOP(sample_02)     //タイムライン「sample_02」の再生を停止する
\SP.GO(CH_1)               //以上の演出を開始


停止しました。


次はタイムラインを１クリック毎に立て続けに再生させてみます。再生途中でクリックしてみてください。


\SP.EM.PLAY(sample_01)   //タイムライン「sample_01」を再生する
\SP.GO(CH_1)               //以上の演出を開始

タイムライン１。

\SP.EM.PLAY(sample_02)   //タイムライン「sample_02」を再生する
\SP.GO(CH_1)               //以上の演出を開始

タイムライン２。

\SP.EM.PLAY(sample_03)   //タイムライン「sample_03」を再生する
\SP.GO(CH_1)               //以上の演出を開始

タイムライン３。


タイムラインの途中で次のタイムラインへ移行しても、その時点のポーズ・表情から流れるように次のポーズ・表情へ移行したと思います。


\SP.ST(emote_test, 500)    //立ち絵としてE-moteファイル「emote_test.psb」を読込、500msかけて表示
\SP.GO(CH_1)               //以上の演出を開始


次に、２つのタイムラインの合成をしてみます。

通常のタイムラインとは別に、差分用のタイムラインを用意して、それを合成することができます。

まず、差分用タイムラインだけを再生してみます。


\SP.EM.DPLAY(差分用_waiting_loop3, 1000, 3)   //差分用タイムライン「差分用_waiting_loop3」を合成比率 3 で 1000ms かけてフェードイン再生する
\SP.GO(CH_1)                                  //以上の演出を開始


再生しました。体が上下に揺れているのがわかると思います。（少し大げさな動きですが分かりやすいように比率を３倍にしています）


ちなみにこれはループ設定のタイムラインなので、延々と再生され続けます。



\SP.EM.DSTOP(差分用_waiting_loop3, 300)       //差分用タイムライン「差分用_waiting_loop3」の再生を300msかけてフェードアウト、停止する
\SP.GO(CH_1)                                  //以上の演出を開始


それでは一旦止めて、次に、通常タイムラインだけを再生してみます。


\SP.EM.PLAY(sample_全自動_test2)              //タイムライン「sample_全自動_test2」を再生する
\SP.GO(CH_1)                                  //以上の演出を開始


再生開始しました。ちなみにこちらもループ設定のタイムラインです。


ではこれに、先ほどの差分用タイムラインを合成して再生してみます。


\SP.EM.DPLAY(差分用_waiting_loop3, 1000, 3)   //差分用タイムライン「差分用_waiting_loop3」を合成比率 3 で 1000ms かけてフェードイン再生する
\SP.GO(CH_1)                                  //以上の演出を開始


再生開始しました。

動きに、常に体の上下の動きが加わっているのが分かるかと思います。

差分タイムラインをうまく組み合わせて合成させることにより、より幅広い表現が可能になるかと思います。


タイムラインの説明は以上です。



\SP.ZA(0, 500)             //不透明度を500msかけて0にする
\SP.GO(CH_1)               //以上の演出を開始
\SP.WA(CH_1)               //演出終了まで待機
\SP.DEL(CH_1)              //CH_1 削除


\FOUT(800)                 //フェードアウト


\GO(EMOTE_SHOUKAI)         //ジャンプ




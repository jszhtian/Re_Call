//==================================================================
//
// E-moteサンプル [ キャラクターの読込・表示 ]
//
// キャラクターの読込、表示、消去、不透明度、トランジション、
//  ブレンドモード
//
//==================================================================
#=EMOTE_キャラクターの読込・表示


\FOUT(0)              //フェードアウト(0msかけて幕を下ろす)
\BG(bg_040, 150, 0)   //背景をZ=150の位置に0msかけて表示
\FIN(600, 23)         //フェードイン(600msかけて、トランジション定義番号23を使って幕を上げる)


それでは、早速E-moteキャラクターを読み込んでみます。


\SP.ST(emote_test, 500)    //立ち絵としてE-moteファイル「emote_test.psb」を読込、500msかけて表示
\SP.ZBXY(50,50,0)          //※定義ファイル行き.
\SP.ZXYZ(0, 150, 200, 0)   //XYZ=(0,150,200)に設置.(正確には(0,0,0)の位置から(0,150,200)の位置へ0msかけて移動をおこなっている)
\SP.GO(CH_1)               //以上の演出を開始


キャラクターを読込、表示しました。

YU-RISでは、通常の立ち絵画像読み込み命令を使って、同様にE-moteキャラクターも読み込むことができます。


\SP.ST(yuu_1a01, 500)      //立ち絵として「yuu_1a01.png」を読込、1000msかけて表示
\SP.ZXYZ(200, 000, 200, 0) //XYZ=(0,150,200)に設置.(正確には(0,0,0)の位置から(0,150,200)の位置へ0msかけて移動をおこなっている)
\SP.GO(CH_2)               //以上の演出を開始


そして読み込み後も、通常立ち絵と全く同じように制御できますので、別の通常立ち絵と混在させたり、


\SP.SZ(-15, 800)           //Zを-10分、800msかけて移動
\SP.GO(CH_1)               //以上の演出を開始


立ち絵の移動や、


\SP.ZA(0, 500)             //不透明度を500msかけて0にする
\SP.GO(CH_2)               //以上の演出を開始
\SP.WA(CH_2)               //演出終了まで待機
\SP.DEL(CH_2)              //CH_2 削除


\WA(500)                   //500msウェイト


\SP.ZA(128, 500)           //不透明度を500msかけて128にする
\SP.GO(CH_1)               //以上の演出を開始


不透明度の変更、



\MD(0x5050a0)              //以降読み込まれる画像の明度を0x5050a0にセット

\SP.ST(emote_test, 0)      //立ち絵としてE-moteファイル「emote_test.psb」を読込、0msで表示
\SP.ZA(256,0)              //不透明度を0msで256にする
\SP.GO(CH_1)               //以上の演出を開始


明度の設定、



\MD()                      //以降読み込まれる画像の明度を元(0x808080)に戻す

\SP.ST(emote_test, 0)      //立ち絵としてE-moteファイル「emote_test.psb」を読込、0msで表示
\SP.BL(1)                  //ブレンドモードを1(=加算)にする
\SP.GO(CH_1)               //以上の演出を開始


ブレンドモードの変更などもおこなうことが出来ます。


\SP.ZA(256,0)              //不透明度を0msで256にする

\SP.BL(0)                           //ブレンドモードを0(=通常)に戻す
\SP.EM.VAR(head_slant  ,  20, 500)  //変数「head_slant」の値を500msかけて20に変化させる
\SP.EM.VAR(head_UD     ,  30, 500)  //変数「head_UD」の値を500msかけて30に変化させる
\SP.EM.VAR(head_LR     ,  28, 500)  //変数「head_LR」の値を500msかけて28に変化させる
\SP.EM.VAR(body_UD     ,  24, 500)  //変数「body_UD」の値を500msかけて24に変化させる
\SP.EM.VAR(face_eye_UD , -20, 300)  //変数「face_eye_UD」の値を300msかけて-20に変化させる
\SP.EM.VAR(face_eyebrow,  30,   0)  //変数「face_eyebrow」の値を0msかけて30に変化させる
\SP.EM.VAR(face_mouth  ,  30,   0)  //変数「face_mouth」の値を0msかけて30に変化させる
\SP.GO(CH_1)                        //以上の演出を開始

\WA(800)

\SP.EM.VAR(face_eye_LR ,  20, 500)  //変数「face_eye_LR」の値を500msかけて20に変化させる
\SP.EM.VAR(face_talk   ,   2, 700)  //変数「face_talk」の値を700msかけて2に変化させる
\SP.GO(CH_1)                        //以上の演出を開始

\WA(900)

\SP.EM.VAR(face_eye_LR  , -10, 500)  //変数「face_eye_LR」の値を500msかけて-10に変化させる
\SP.EM.VAR(face_talk    ,   1, 500)  //変数「face_talk」の値を500msかけて1に変化させる
\SP.GO(CH_1)                         //以上の演出を開始


このような感じで、E-moteを使ったADVやノベルがYU-RISで簡単に作ることが出来るようになりました。


\SP.ZA(0, 500)             //不透明度を500msかけて0にする
\SP.GO(CH_1)               //以上の演出を開始
\SP.WA(CH_1)               //演出終了まで待機
\SP.DEL(CH_1)              //CH_1 削除


キャラクターを消去しました。


読込・表示の説明は以上です。


\FOUT(800)                 //フェードアウト


\GO(EMOTE_SHOUKAI)        //ジャンプ




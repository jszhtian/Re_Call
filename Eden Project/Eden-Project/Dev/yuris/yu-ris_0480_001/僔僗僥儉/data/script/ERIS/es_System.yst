//====================================================================
//
//■SYSTEM
//
//====================================================================

//■スタティック変数宣言
{
}

//----------------------------------------------------------------
//■起動チェック
//----------------------------------------------------------------
#=es.SYSTEMStartCheck
{

	//----------------------------------------------------------
	// 65536色以上あるか色数をチェック
	//----------------------------------------------------------
		IF[@T(17,03)>0]
			IF[@_SCREEN_COLOR<=8]
				\_mes("動作には65536色以上表示可能な環境が必要です。")
				END[]
			IFEND[]
		IFEND[]

	//----------------------------------------------------------
	// サウンド機能が利用可能かどうかチェック
	//----------------------------------------------------------
		IF[@T(17,04)>0]
			IF[@_OS_SOUND==0]
				\_mes("サウンド機能がないため起動できません。")
				END[]
			IFEND[]
		IFEND[]

	//----------------------------------------------------------
	// 読み取り専用か否かチェック
	//----------------------------------------------------------
		IF[@T(17,05)>0]
			IF[@_MEDIA_READONLY==1]
				\_mes("読み取り専用メディア、または書き込み権限のない環境からの起動です。")
				END[]
			IFEND[]
		IFEND[]

	return[]
}


//----------------------------------------------------------------
//■各初期化
//----------------------------------------------------------------
#=es.Initialize
{
	//-----------------------------------------------------------
	// ゲームフラグ初期化
	//-----------------------------------------------------------
		\LSD.INIT

	//-----------------------------------------------------------
	// 直前の選択肢データ削除
	//-----------------------------------------------------------
		\SAVEDEL(@es.sdprevselno)
		\SAVEDEL(@es.sdprevselno+1)

	//-----------------------------------------------------------
	// 画面彩度明度初期化
	//-----------------------------------------------------------
//		\SAIDO()
//		\MEIDO()

	//	\es.CGDEL(1)
	//	\es.CGDEL(2)
	//	\es.CGDEL(3)
	//	\es.CGDEL(4)
	//	\es.CGDEL(5)
	//	\es.CGDEL(6)

///x
	//メニューチップトランジション用
//	GOSUB[#=es.BT.TR.INIT pstr="cgsys/main/button/tip_btnback_tr"]


	return[]
}


//----------------------------------------------------------------
//■乱数
//----------------------------------------------------------------
#=es.RND
{
	INT[@ret]

	INT[@val1=@_PINT(1)]
	INT[@val2=@_PINT(2)]
	INT[@per1=@_PINT(3)]
	INT[@per2=@_PINT(4)]
	INT[@val3=@_PINT(5)]
	INT[@val4=@_PINT(6)]

	IF[@per2>@per1] @per2=@per1 IFEND[] //3分の4 → 3分の3にする

	\_rnd(1,@per1)
	IF[@per1>0 && @per2>0 && @_RINT(1)<=@per2]
	{
		\_rnd(@val3,@val4)(@ret)
	}
	ELSE[]
	{
		\_rnd(@val1,@val2)(@ret)
	}
	IFEND[]

	return[rint=@ret]
}


//----------------------------------------------------------------
//■タイトルバーセット
//----------------------------------------------------------------
#=es.SYS.CAPTION.SET
{
	$es.WindowCaption1 = $_PSTR(1)
	$es.WindowCaption2 = $_PSTR(2)

	GOSUB[#=es.CAPTION.REFRESH]
	return[]
}

#=es.CAPTION.SET
{
	$es.WindowCaption1 = $_PSTR(1)

	GOSUB[#=es.CAPTION.REFRESH]
	return[]
}

#=es.CAPTION.REFRESH
{
	STR[$mDCap]
	IF[@_DEBUGMODE] $mDCap=$T(37,01) IFEND[]

///xPatch
	WINDOW[NO=0 CAPTION=$es.WindowCaption1+$es.WindowCaption2+$mDCap]

	return[]
}


///x
//----------------------------------------------------------------
//■マウスカーソルセット
//----------------------------------------------------------------
#=es.MCURSOR.SET
{

	/*
		IF[$T(17,11)!=""]
			CG[ID=ES.MCURSOR Z=1 E=0 FILE=$T(17,11)]
			MOUSE[ID=ES.MCURSOR HX=@T(17,12) HY=@T(17,13)]
		IFEND[]
	*/
	INT[@no]
	IF[@es.GSD(110,14)>0]
	{
		@no = @es.GSD(110,15)*50+@es.GSD(110,14)*10

		CG[ID=ES.MCURSOR Z=1 E=0 FILE="CGSYS/"+$T(17,@no+1)]
		MOUSE[ID=ES.MCURSOR HX=@T(17,@no+2) HY=@T(17,@no+3)]
		CGEND[ID=ES.MCURSOR]
	}
	ELSE[]
	{
		MOUSE[CURSOR=0]
	}
	IFEND[]

	return[]
}


///x
//----------------------------------------------------------------
//■ウィンドウリサイズ設定セット
//----------------------------------------------------------------
#=es.WINDOWRESIZE.SET
{
	IF[@es.GSD(110,16)==1]
	{
		WINDOW[NO=0 RESIZE=1]
	}
	ELSE[]
	{
		WINDOW[NO=0 RESIZE=0]
	}
	IFEND[]

	\GSD.REFLECT

	return[]
}


//----------------------------------------------------------------
//■ウィンドウサイズ初期化
//----------------------------------------------------------------
#=es.WINDOWSIZEINIT.SET
{
	INT[@SC1]
	INT[@SC2]
	WINDOWINFO[NO=0 FULLSCREEN=1 LET=@SC1]
	WINDOWINFO[NO=0 FULLSCREEN2=1 LET=@SC2]

//	IF[@SC1==1]
	IF[@SC1==999]
		GO[#=es.WINDOWSIZEINIT.SET.LEnd]
	//最大化状態ならそれを解除
	ELSE[@SC2==1]
		WINDOW[NO=0 MAXIMIZE=1]
		WAIT[FRAME=1]
	IFEND[]

	//ウィンドウサイズ初期化
	@es.WSX = @es.GSX2
	@es.WSY = @es.GSY2
	WINDOW[NO=0 WSX=@es.WSX WSY=@es.WSY]

	#=es.WINDOWSIZEINIT.SET.LEnd
	return[]
}


//----------------------------------------------------------------
//■画像縮小用リサイズ(遅いけど綺麗)
//  一応拡大も対応するけど画質は普通
//$_PSTR(1):参照元レイヤID
//$_PSTR(2):出力先レイヤID (参照元と同じIDでも可)
//@_PINT(3):リサイズ後の幅
//@_PINT(4):リサイズ後の高さ
//----------------------------------------------------------------
#=es.CG.RESIZE
{
	STR[$SRCID=$_PSTR(1)]
	STR[$DSTID=$_PSTR(2)]
	INT[@DSX=@_PINT(3)]
	INT[@DSY=@_PINT(4)]
	INT[@SSX];CGINFO[ID=$SRCID SX=1 LET=@SSX] //リサイズ前の幅
	INT[@SSY];CGINFO[ID=$SRCID SY=1 LET=@SSY] //リサイズ前の高さ
	INT[@flg]

	LOOP[]
	{
		@flg=(@SSX/2>@DSX && @SSY/2>@DSY)

		@SSX/=2;IF[@SSX<@DSX] @SSX=@DSX IFEND[]
		@SSY/=2;IF[@SSY<@DSY] @SSY=@DSY IFEND[]

		IF[@flg]
			CGACT[ID=$SRCID ID2=$DSTID SIZE4=1 SX=@SSX SY=@SSY]
		ELSE[]
			CGACT[ID=$SRCID ID2=$DSTID SIZE2=1 SX=@SSX SY=@SSY]
		IFEND[]

		$SRCID=$DSTID	//２回目以降はDSTIDをいじるのみとなる.

		IF[@SSY==@DSY && @SSX==@DSX] LOOPBREAK[] IFEND[]
	}
	LOOPEND[]

	return[]
}



//----------------------------------------------------------------
//■WEBページ起動処理
//----------------------------------------------------------------
#=ES.STARTWEB
{
	STR[$url=$_PSTR(1)]

	@es.GSD(141,02) = 0
	WINDOW[NO=0 FULLSCREEN=@es.GSD(141,02)]
//	WINDOW[NO=0 Z=@es.GSD(141,02)] //前面or常に手前
	WINDOW[NO=0 Z=@es.GSD(110,01)] //前面or常に手前

	FILEACT[EXEC=1 FILE="IExplore" PARAM=$url]	//IE固定
//	FILEACT[EXEC=1 FILE=$url]					//既定のブラウザ(ブラウザによっては起動せず)

	WAIT[FRAME=60]

	return[]
}


//----------------------------------------------------------------
//■システムボイス再生終了待ち
//----------------------------------------------------------------
#=es.SVO.ENDWAIT
{
	INT[@SkipForbit=@_PINT(1)] //スキップ禁止フラグ(==1)

	LOOP[]
	{
		\SVO.PCHK
		IF[@_RINT(1)==0] LOOPBREAK[] IFEND[] //再生終了したら
		IF[@SkipForbit==0]
			IF[@_MOUSE_L==1 || @_KEY_ENTER==1 || @_KEY_ESC==1] LOOPBREAK[] IFEND[] //キー入力でスキップ
		IFEND[]
		WAIT[FRAME=1]
	}
	LOOPEND[]

	return[]
}



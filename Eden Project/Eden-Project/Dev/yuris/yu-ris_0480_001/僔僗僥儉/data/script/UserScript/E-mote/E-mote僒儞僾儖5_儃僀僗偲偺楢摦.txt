//==================================================================
//
// E-moteサンプル [ ボイスとの連動 ]
//
//==================================================================
#=EMOTE_ボイスとの連動


\FOUT(0)            //フェードアウト(0msかけて幕を下ろす)
\BG(bg_040, 150, 0) //背景をZ=150の位置に0msかけて表示
\FIN(600, 23)       //フェードイン(600msかけて、トランジション定義番号23を使って幕を上げる)


\SP.ST(emote_test, 500)    //立ち絵としてE-moteファイル「emote_test.psb」を読込、500msかけて表示
\SP.ZBXY(50,50,0)          //※定義ファイル行き.
\SP.ZXYZ(0, 150, 200, 0)   //XYZ=(0,150,200)に設置.(正確には(0,0,0)の位置から(0,150,200)の位置へ0msかけて移動をおこなっている)
\SP.GO(CH_1)               //以上の演出を開始


ボイスとの連動機能を紹介します。


ボイスが再生された際に、現在表示されているE-moteキャラクターに対して、自動的に口パクをおこなわせることが出来ます。


では実際にやってみます。



\SP.EM.VO(face_talk, 2)    //変数「face_talk」をボイス連動箇所、キャラクター番号２番として立ち絵に登録する
\SP.GO(CH_1)               //以上の演出を開始


\VO(ERI_0030)              //ボイス再生
ボイス再生。


また、タイムライン再生中でも連動可能です。（※タイムライン側で口を制御していないければ）


\SP.EM.PLAY(sample_00)     //タイムライン「sample_00」を再生する
\SP.GO(CH_1)               //以上の演出を開始


\WA(800)                   //ウェイト


\VO(ERI_0030)              //ボイス再生
タイムライン再生＋ボイス連動。


このような感じです。いかがでしょうか。


E-moteキャラクターならとても簡単に自動で口パクをさせることができるので、ボイス有りの作品を作る場合はぜひとも使ってみてください。



\SP.ZA(0, 500)             //不透明度を500msかけて0にする
\SP.GO(CH_1)               //以上の演出を開始
\SP.WA(CH_1)               //演出終了まで待機
\SP.DEL(CH_1)              //CH_1 削除


\FOUT(800)                 //フェードアウト


\GO(EMOTE_SHOUKAI)         //ジャンプ




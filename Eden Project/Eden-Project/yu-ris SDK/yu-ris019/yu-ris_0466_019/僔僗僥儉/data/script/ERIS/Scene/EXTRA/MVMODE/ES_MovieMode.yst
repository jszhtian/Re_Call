//=========================================================================
//
//■ムービー鑑賞モード
//
//=========================================================================

//■スタティック変数
{
	S_STR[$BID="ES.MVMODE."]

	S_INT[@D.NUM]

	S_INT[@D.FLAG(\.MVMODE.MAX+1)]
	S_STR[$D.FILE(\.MVMODE.MAX+1)]
	S_INT[@D.GROUP(\.MVMODE.MAX+1)]
	S_INT[@D.NO(\.MVMODE.MAX+1)]
	S_INT[@D.PAT(\.MVMODE.MAX+1)]

	S_STR[$TMP.FILE]
	S_STR[$TMP.THUMB]


	S_INT[@DCG.NUM] //鑑賞数
	S_INT[@DCG.MAX] //総数
	S_INT[@CG.MAX]  //ゞ

	S_INT[@PLATENO(\.MVMODE.CG.MAX+1, 2)]
	S_INT[@GRP.NO]
	S_INT[@PAGE.NO]
	S_INT[@PAGE.NUM]
	S_INT[@CG.NO]
	S_INT[@CG.NUM]

	S_INT[@THUMB.NUM]

	S_INT[@Z.NUM  =60000000000000+3]

	S_INT[@es.MVMODE.GRP]  //鑑賞バック用
	S_INT[@es.MVMODE.PAGE] //鑑賞バック用

	S_INT[@FlagMax]
	S_INT[@FlagNum]

	S_INT[@MOVIESTARTTIME]

}


//----------------------------------------------------------------
//■LOOP
//----------------------------------------------------------------
#=ES.MVMODE.LOOP
{
	IF[@es.STATE==0]
	{
		@es.STATE=1
	}
	IFEND[]
	IF[@es.STATE==1] //INIT
	{
		@GRP.NO  = 1
		@PAGE.NO = 1

		@THUMB.NUM=@T(33,03)

		//ＢＧＭ処理
		\BGM($T(33,02))

		//----------------------------------------------------------------
		// 初期化
		//----------------------------------------------------------------
			GOSUB[#=es.MVMODE.CGINIT]

		@es.STATE=2
		@es.STATE2=(400) //フェード時間
	}
	IFEND[]
	IF[@es.STATE==2]
	{
		//----------------------------------------------------------------
		// ページ更新
		//----------------------------------------------------------------
			GOSUB[#=es.MVMODE.REF]

		@es.STATE=3
	}
	IFEND[]
	IF[@es.STATE==3] //FADE
	{
		\SS.FADE(@es.STATE2)
		@es.STATE=100
	}
	IFEND[]


	IF[@es.STATE==101] //MOVIE-VIEW-INIT
	{
			//----------------------------------------------------------------
			// スクリーンショット
			//----------------------------------------------------------------
			//	\es.CGTSS  //1まずは最前面にSS
			//	\es.CGSSS  //2それをまたSS
			//	\es.CGTDEL //3最前面削除
				\es.CGSSS

			//----------------------------------------------------------------
			// ボタン非表示
			//----------------------------------------------------------------
		//		\BT.ES0.ALL
				\BT.ACTIVEGROUP.ADD( 2 )

			//----------------------------------------------------------------
			// 各種画像非表示
			//----------------------------------------------------------------
				GOSUB[#=es.MVMODE.Num.Hide]

			//----------------------------------------------------------------
			// フェードアウト
			//----------------------------------------------------------------
				\es.CGTBLACK
				\es.CGTFADEIN(400)

			//----------------------------------------------------------------
			// ウェイト
			//----------------------------------------------------------------
				WAIT[TIME=500]

			//----------------------------------------------------------------
			// ムービー再生
			//----------------------------------------------------------------
				INT[@codeccheck]
				MOVIEINFO[CODEC=$D.FILE(@PLATENO(@CG.NO,1)) LET=@codeccheck]
				IF[@codeccheck>1]
				{
					IF[@_DEBUGMODE && @T(17,10)==1]
						\_dmes("ムービーファイル [ "+$D.FILE(@PLATENO(@CG.NO,1))+" ] が MPEG-1コーデックではありません。"+$_CHR(13)+$_CHR(13)+"MPEG-1コーデックでない場合、環境によって再生できない場合がありますので、"+$_CHR(13)+"MPEG-1コーデックのムービーを使用してください。", "エラー")
					IFEND[]
				}
				IFEND[]
				IF[@codeccheck==0]
				{
					IF[@_DEBUGMODE]
						\_dmes("ムービーファイル [ "+$D.FILE(@PLATENO(@CG.NO,1))+" ] が存在しません。", "エラー")
					IFEND[]

					@es.STATE=103
					IFBREAK[LV=2]
				}
				IFEND[]

				INT[@vol]
				\VOL.BGM.LS.GET(@vol)
///x
//				GOSUB[#=es.VOL.MASTER.GET];@vol=@vol*@_RINT(1)/256
//@vol*=2
//\_range(@vol, 0, 256)
				MOVIE[FILE=$D.FILE(@PLATENO(@CG.NO,1))]
				MOVIE[VOLUME=@vol] //BGM音量に合わせる
				MOVIE[PLAY=1]

			//----------------------------------------------------------------
			// 再生開始時間を取得
			//----------------------------------------------------------------
				@MOVIESTARTTIME=@_OS_TIME(1)

			@es.MOVIEFLAG=1

		@es.STATE=102
	}
	IFEND[]
	IF[@es.STATE==102] //クリック待ち or 再生終了待ち
	{
			INT[@mRET]
			MOVIEINFO[PLAY=1 LET=@mRET]
			IF[@mRET==0] @es.STATE=103 IFEND[]
	}
	IFEND[]
	IF[@es.STATE==103] //MOVIE-END
	{

			@es.MOVIEFLAG=0

///xフェードイン中にボタンが聞いてしまっているので無効にする機能を作る

			//----------------------------------------------------------------
			// ムービー再生終了
			//----------------------------------------------------------------
				MOVIEEND[]

			//----------------------------------------------------------------
			// ボタン表示
			//----------------------------------------------------------------
		//		\BT.ES1.ALL
				\BT.ACTIVEGROUP.ADD( -2 )

			//----------------------------------------------------------------
			// ウェイト
			//----------------------------------------------------------------
				WAIT[TIME=500]

			//----------------------------------------------------------------
			// 各種画像表示
			//----------------------------------------------------------------
				GOSUB[#=es.MVMODE.Num.Show]

			//----------------------------------------------------------------
			// ページ更新
			//----------------------------------------------------------------
				GOSUB[#=es.MVMODE.REF]

			//----------------------------------------------------------------
			// フェードイン
			//----------------------------------------------------------------
				\es.CGTFADEOUT(400)
				\es.CGSDEL


		@es.STATE=100
	}
	IFEND[]

	return[]
}


//-----------------------------------------------------------------
//■ページ初期化
//-----------------------------------------------------------------
#=es.MVMODE.CGINIT
{
	//----------------------------------------------------------------
	// 数字読込
	//----------------------------------------------------------------
		GOSUB[#=es.MVMODE.Num.Load]

	//----------------------------------------------------------------
	// 
	//----------------------------------------------------------------
		GOSUB[#=es.MVMODE.CG.VIEW.GET]	@DCG.NUM = @_RINT(1)	//鑑賞数
		GOSUB[#=es.MVMODE.CG.MAX.GET]	@DCG.MAX = @_RINT(1)	//総数

	//----------------------------------------------------------------
	//
	//----------------------------------------------------------------
		\BT.ON($BID+"BTN.TAB.MVMODE") //MVMODEタブ

	return[]
}



//-----------------------------------------------------------------
//■鑑賞情報取得
//@_PINT(1):グループ番号
//-----------------------------------------------------------------
#=ES.MVMODE.GETINFO
{
	INT[@grp=@_PINT(1)] //情報取得するグループ番号

	//----------------------------------------------------------------
	// 初期化
	//----------------------------------------------------------------
		LOOP[SET=\.MVMODE.CG.MAX]
		{
			@PLATENO(@_LC-1,0)=0
			@PLATENO(@_LC-1,1)=0
		}
		LOOPEND[]

	//----------------------------------------------------------------
	// 条件が合うものを登録していく.
	//----------------------------------------------------------------
		@CG.NUM  = 0
		@FlagMax = 0
		@FlagNum = 0

		LOOP[SET=@D.NUM]
		{
			IF[@D.GROUP(@_LC)==@grp]
			{
				@PLATENO(@D.NO(@_LC), @D.PAT(@_LC)) = @_LC

				//最大NOを調べる.
				IF[@CG.NUM < @D.NO(@_LC)] @CG.NUM=@D.NO(@_LC) IFEND[]

				//
				IF[@D.NO(@_LC)>0 && @D.PAT(@_LC)>0] @FlagMax+=1 IFEND[]
				IF[@D.FLAG(@_LC)>0] @FlagNum+=1 IFEND[]

			}
			IFEND[]
		}
		LOOPEND[]

		//ページ数を取得
		@PAGE.NUM = (@CG.NUM+@THUMB.NUM-1) / @THUMB.NUM
		IF[@PAGE.NUM==0] @PAGE.NUM=1 IFEND[]

	return[]
}


//-----------------------------------------------------------------
//■ページ更新
//-----------------------------------------------------------------
#=es.MVMODE.REF
{
	//---------------------------------------------------------------------
	// 鑑賞情報取得
	//---------------------------------------------------------------------
		GOSUB[#=ES.MVMODE.GETINFO pint=@GRP.NO]

//立ち絵表示を切り替える場合
/*
	LOOP[SET=(99)] \BT.ES0($BID+"TIP.CHARA", @_LC) LOOPEND[]
//	IF[@GRP.NO==1]
	IF[0]
		IF[@PAGE.NO==1] \BT.ES1($BID+"TIP.CHARA", 1) IFEND[]
		IF[@PAGE.NO==2] \BT.ES1($BID+"TIP.CHARA", 9) IFEND[]
	ELSE[]
		\BT.ES1($BID+"TIP.CHARA", @GRP.NO)
	IFEND[]
*/

	//---------------------------------------------------------------------
	// 表示
	//---------------------------------------------------------------------
		INT[@x]INT[@y]
		INT[@CGViewNum]
		INT[@exist]

		LOOP[SET=@THUMB.NUM]
		{
			GOSUB[#=es.BT.A0 pstr=$BID+"BTN.PLATE" pint2=@_LC]
			GOSUB[#=es.BT.A0 pstr=$BID+"BTN.THUMB" pint2=@_LC]
			\BT.CGCLEAR($BID+"BTN.THUMB", @_LC)
			\BT.CGCLEAR.OVER($BID+"BTN.THUMB", @_LC)

			IF[@PLATENO((@PAGE.NO-1)*@THUMB.NUM+@_LC,0)>0]
		//	IF[@PLATENO((@PAGE.NO-1)*@THUMB.NUM+@_LC,0)>=0] //テスト用
			{
				GOSUB[#=es.BT.A1 pstr=$BID+"BTN.PLATE" pint2=@_LC]
				GOSUB[#=es.BT.A1 pstr=$BID+"BTN.THUMB" pint2=@_LC]

				//鑑賞登録チェック
				GOSUB[#=es.MVMODE.CG.VIEW.CHK pint=(@PAGE.NO-1)*@THUMB.NUM+@_LC]
				@CGViewNum = @_RINT(1)

				//ベースＣＧ１枚
				@CG.NUM = 1

				IF[@CGViewNum>0]
		//		IF[@CGViewNum>=0] //テスト用
				{
					CG[ID="_TMP_OF" Z=1 E=0 FILE=$T(33,01)+$D.FILE(@PLATENO((@PAGE.NO-1)*@THUMB.NUM+@_LC,0)) ]
					CG[ID="_TMP_OV" Z=1 E=0 FILE=""]
					CGACT[ID="_TMP_OF" ID2="_TMP_OV" COPY2=1]
					CGACT[ID="_TMP_OF" AKARUSA=1 SET=@T(33,04)*128/256]
					CGACT[ID="_TMP_OV" AKARUSA=1 SET=@T(33,05)*128/256]
					GOSUB[#=es.BT.ID.OFF.GET pstr=$BID+"BTN.THUMB" pint2=@_LC]
			CGINFO[ID=$_RSTR(1) EXIST=1 LET=@exist]IF[@exist]
					CGACT[ID="_TMP_OF" ID2=$_RSTR(1) MERGE2=1 X=0 Y=0]
			IFEND[]
					GOSUB[#=es.BT.ID.OVER.GET pstr=$BID+"BTN.THUMB" pint2=@_LC]
			CGINFO[ID=$_RSTR(1) EXIST=1 LET=@exist]IF[@exist]
					CGACT[ID="_TMP_OV" ID2=$_RSTR(1) MERGE2=1 X=0 Y=0]
			IFEND[]
					CGEND[ID="_TMP_OF"]
					CGEND[ID="_TMP_OV"]

				}
				IFEND[]
			}
			IFEND[]
		}
		LOOPEND[]


	//----------------------------------------------------------------
	// 数字表示
	//----------------------------------------------------------------
		GOSUB[#=es.MVMODE.Num1 PINT=@PAGE.NO PINT2=@PAGE.NUM]
		GOSUB[#=es.MVMODE.Num2 PINT=@DCG.NUM PINT2=@DCG.MAX]	//鑑賞数、鑑賞率
		GOSUB[#=es.MVMODE.Num2G PINT=@FlagNum PINT2=@FlagMax]	//鑑賞数、鑑賞率(グループ毎)

	//----------------------------------------------------------------
	// ←→ボタン有効無効
	//----------------------------------------------------------------
		IF[@PAGE.NO==1]
			\BT.N1($BID+"BTN.PREVPAGE")
		ELSE[]
			\BT.N0($BID+"BTN.PREVPAGE")
		IFEND[]
		IF[@PAGE.NO==@PAGE.NUM]
			\BT.N1($BID+"BTN.NEXTPAGE")
		ELSE[]
			\BT.N0($BID+"BTN.NEXTPAGE")
		IFEND[]


	//即表示されるように
	GOSUB[#=es.BT.DRAW]


	return[]
}


//--------------------------------------------------------------------
//■総鑑賞数を計算
//--------------------------------------------------------------------
#=es.MVMODE.CG.VIEW.GET
{
	INT[@r]

	LOOP[SET=@D.NUM]
	{
		IF[@D.PAT(@_LC)>0 && @D.FLAG(@_LC)>0] @r+=1 IFEND[]
	}
	LOOPEND[]

	return[rint=@r]
}


//--------------------------------------------------------------------
//■差分ムービーを何枚鑑賞したかchk.
//--------------------------------------------------------------------
#=es.MVMODE.CG.VIEW.CHK
{
	INT[@r]
	IF[@D.FLAG(@PLATENO(@_PINT(1),1)) > 0] @r+=1 IFEND[]
	return[rint=@r]
}


//--------------------------------------------------------------------
//■鑑賞リスト登録
//--------------------------------------------------------------------
#=es.MVMODE.FILE.SET
{
//	\_strlower($_PSTR(1));$TMP.FILE=$_RSTR(1)	//大文字小文字判別するならこっち。でもそもそもラベルなのでこっちはまずい
	$TMP.FILE = $_PSTR(1)
	return[]
}

#=es.MVMODE.THUMB.SET
{
	$TMP.THUMB = $_PSTR(1)
	return[]
}

#=es.MVMODE.DEF.SET
{
	@D.NUM+=1

	//サムネイル登録
	$D.FILE( @D.NUM )  = $TMP.THUMB
	@D.GROUP( @D.NUM ) = @_PINT(1)
	@D.NO( @D.NUM )    = @_PINT(2)
	@D.PAT( @D.NUM )   = 0

	@D.NUM+=1

	//ラベル登録
	$D.FILE( @D.NUM )  = $TMP.FILE
	@D.GROUP( @D.NUM ) = @_PINT(1)
	@D.NO( @D.NUM )    = @_PINT(2)
	@D.PAT( @D.NUM )   = 1

	@CG.MAX+=1

	$TMP.THUMB = ""
	$TMP.FILE  = ""

	return[]
}



//--------------------------------------------------------------------
//■鑑賞フラグ登録
//--------------------------------------------------------------------
#=es.MVMODE.CG.FLAG.SET
{
	STR[$fn=$_PSTR(1)]

	LOOP[SET=@D.NUM]
	{
		IF[@D.PAT( @_LC )>0]
		{
			IF[$fn==$D.FILE(@_LC)]
			{
				//\_dmes("["+$fn+"] 発見しました。")
				@D.FLAG(@_LC) = 1  //ムービーフラグON
			}
			IFEND[]
		}
		IFEND[]
	}
	LOOPEND[]

	return[]
}


//--------------------------------------------------------------------
//■鑑賞フラグ強制ON/OFF
//--------------------------------------------------------------------
#=es.MVMODE.MV.SETALL
{
	IF[@_PINT(1)==0 || @_PINT(1)==1]
	{
		LOOP[SET=@D.NUM]
		{
			IF[@D.PAT( @_LC )>0]
			{
				@D.FLAG(@_LC) = @_PINT(1)  //ムービーフラグON/OFF
			}
			IFEND[]
		}
		LOOPEND[]
	}
	IFEND[]

	return[]
}


//--------------------------------------------------------------------
//■鑑賞フラグ登録チェック
//--------------------------------------------------------------------
#=es.MVMODE.CG.FLAG.CHK
{
	INT[@ret]

	STR[$fn=$_PSTR(1)]

	LOOP[SET=@D.NUM]
	{
		IF[@D.PAT( @_LC )>0]
		{
			IF[$fn==$D.FILE(@_LC)]
			{
				@ret=@D.FLAG(@_LC)
				//\_dmes("["+$fn+"] 発見しました。")
			}
			IFEND[]
		}
		IFEND[]
	}
	LOOPEND[]

	return[rint=@ret]
}



#=ES.MVMODE.END
{
	// 各種画像消去

		//数値画像
		GOSUB[#=es.MVMODE.Num.End]

	return[]
}


//------------------------------------------------
//■ページ数表示
//------------------------------------------------
#=es.MVMODE.Num1
{
	INT[@Num1=@_PINT(1)]
	INT[@Num2=@_PINT(2)]

	GOSUB[#=es.NUM.Enabled pint=11 pint2=@Num1]	//現在の頁
	GOSUB[#=es.NUM.Enabled pint=12 pint2=@Num2]	//総頁数

	return[]
}

//------------------------------------------------
//■鑑賞数、鑑賞率表示
//------------------------------------------------
#=es.MVMODE.Num2
{
	INT[@Num1=@_PINT(1)]
	INT[@Num2=@_PINT(2)]
	INT[@VAL=@Num1*100/@Num2]
	FLT[@f=@Num1*100.0/@Num2];IF[@f>0 && @f<2.0] @VAL=1 IFEND[] // 0<n<2の時を1にする調整.

	GOSUB[#=es.NUM.Enabled pint=13 pint2=@Num1]	//鑑賞数
	GOSUB[#=es.NUM.Enabled pint=14 pint2=@Num2]	//総数
	GOSUB[#=es.NUM.Enabled pint=15 pint2=@VAL]	//鑑賞率

	return[]
}

//------------------------------------------------
//■鑑賞数、鑑賞率表示(グループ毎)
//------------------------------------------------
#=es.MVMODE.Num2G
{
	INT[@Num1=@_PINT(1)]
	INT[@Num2=@_PINT(2)]
	INT[@VAL=@Num1*100/@Num2]
	FLT[@f=@Num1*100.0/@Num2];IF[@f>0 && @f<2.0] @VAL=1 IFEND[] // 0<n<2の時を1にする調整.

	GOSUB[#=es.NUM.Enabled pint=16 pint2=@Num1]	//鑑賞数
	GOSUB[#=es.NUM.Enabled pint=17 pint2=@Num2]	//総数
	GOSUB[#=es.NUM.Enabled pint=18 pint2=@VAL]	//鑑賞率

	return[]
}

//------------------------------------------------
//■画像読込
//------------------------------------------------
#=es.MVMODE.Num.Load
{
	//現在のページ
	\NUM.FILE(11, $T(33,11))
	\NUM.XYZ(11, @T(33,12), @T(33,13), @Z.NUM)
	\NUM.SX(11, @T(33,14))
	\NUM.DIG(11, @T(33,15), @T(33,16))
	\NUM.MAX(11, 99)
	\NUM.MIN(11, 0)
	\NUM.VAL(11, 0)
	\NUM.SET(11)

	//総ページ
	\NUM.FILE(12, $T(33,17))
	\NUM.XYZ(12, @T(33,18), @T(33,19), @Z.NUM)
	\NUM.SX(12, @T(33,20))
	\NUM.DIG(12, @T(33,21), @T(33,22))
	\NUM.MAX(12, 99)
	\NUM.MIN(12, 0)
	\NUM.VAL(12, 0)
	\NUM.SET(12)

	//鑑賞数
	\NUM.FILE(13, $T(33,23))
	\NUM.XYZ(13, @T(33,24), @T(33,25), @Z.NUM)
	\NUM.SX(13, @T(33,26))
	\NUM.DIG(13, @T(33,27), @T(33,28))
	\NUM.MAX(13, 9999)
	\NUM.MIN(13, 0)
	\NUM.VAL(13, 0)
	\NUM.SET(13)

	//総数
	\NUM.FILE(14, $T(33,29))
	\NUM.XYZ(14, @T(33,30), @T(33,31), @Z.NUM)
	\NUM.SX(14, @T(33,32))
	\NUM.DIG(14, @T(33,33), @T(33,34))
	\NUM.MAX(14, 9999)
	\NUM.MIN(14, 0)
	\NUM.VAL(14, 0)
	\NUM.SET(14)

	//鑑賞率
	\NUM.FILE(15, $T(33,35))
	\NUM.XYZ(15, @T(33,36), @T(33,37), @Z.NUM)
	\NUM.SX(15, @T(33,38))
	\NUM.DIG(15, @T(33,39), @T(33,40))
	\NUM.MAX(15, 100)
	\NUM.MIN(15, 0)
	\NUM.VAL(15, 0)
	\NUM.SET(15)

	//鑑賞数(グループ毎)
	\NUM.FILE(16, $T(33,41))
	\NUM.XYZ(16, @T(33,42), @T(33,43), @Z.NUM)
	\NUM.SX(16, @T(33,44))
	\NUM.DIG(16, @T(33,45), @T(33,46))
	\NUM.MAX(16, 9999)
	\NUM.MIN(16, 0)
	\NUM.VAL(16, 0)
	\NUM.SET(16)

	//総数(グループ毎)
	\NUM.FILE(17, $T(33,47))
	\NUM.XYZ(17, @T(33,48), @T(33,49), @Z.NUM)
	\NUM.SX(17, @T(33,50))
	\NUM.DIG(17, @T(33,51), @T(33,52))
	\NUM.MAX(17, 9999)
	\NUM.MIN(17, 0)
	\NUM.VAL(17, 0)
	\NUM.SET(17)

	//鑑賞率(グループ毎)
	\NUM.FILE(18, $T(33,53))
	\NUM.XYZ(18, @T(33,54), @T(33,55), @Z.NUM)
	\NUM.SX(18, @T(33,56))
	\NUM.DIG(18, @T(33,57), @T(33,58))
	\NUM.MAX(18, 100)
	\NUM.MIN(18, 0)
	\NUM.VAL(18, 0)
	\NUM.SET(18)

	return[]
}


//------------------------------------------------
//■数値画像表示
//------------------------------------------------
#=es.MVMODE.Num.Show
{
	LOOP[SET=8] GOSUB[#=es.NUM.Enabled pint=11+@_LC-1] LOOPEND[]
	return[]
}

//------------------------------------------------
//■数値画像非表示
//------------------------------------------------
#=es.MVMODE.Num.Hide
{
	LOOP[SET=8] GOSUB[#=es.NUM.Disabled pint=11+@_LC-1] LOOPEND[]
	return[]
}

//------------------------------------------------
//■画像消去
//------------------------------------------------
#=es.MVMODE.Num.End
{
	LOOP[SET=8] GOSUB[#=es.NUM.DEL pint=11+@_LC-1] LOOPEND[]
	return[]
}

//------------------------------------------------
//■セーブ
//------------------------------------------------
#=es.MVMODE.FILESAVE
{
	SAVE[DNO=@_PINT(1) SET=@D.FLAG()]
	return[]
}

//------------------------------------------------
//■ロード
//------------------------------------------------
#=es.MVMODE.FILELOAD
{
	LOAD[FILE="gsd.sd" DNO=@_PINT(1) LET=@D.FLAG()]
	return[]
}


//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//■ボタン処理
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

//■[タブ・ＣＧ]ボタン -----------------------------------------------
//
#=ES.MVMODE.BTN.TAB.CGMODE.ON
{
	\es.S.GO("ES.CGMODE", 1, 0); return[]
}

//■[タブ・回想]ボタン -----------------------------------------------
//
#=ES.MVMODE.BTN.TAB.RPMODE.ON
{
	\es.S.GO("ES.RPMODE", 1, 0) ;return[]
}

//■[タブ・ＢＧＭ]ボタン ---------------------------------------------
//
#=ES.MVMODE.BTN.TAB.BGMMODE.ON
{
	\es.S.GO("ES.BGMMODE", 1, 0); return[]
}

//■[タブ・ムービー]ボタン -------------------------------------------
//
#=ES.MVMODE.BTN.TAB.MVMODE.INIT
{
	\BT.ON($es.BID); return[]
}

//■[グループ]ボタン -------------------------------------------------
//
#=ES.MVMODE.BTN.GROUP.INIT
{
	@GRP.NO  = 1
	@PAGE.NO = 1
	IF[@es.MVMODE.GRP  > 0] @GRP.NO  = @es.MVMODE.GRP  IFEND[]
	IF[@es.MVMODE.PAGE > 0] @PAGE.NO = @es.MVMODE.PAGE IFEND[]
	@es.MVMODE.GRP  = 0
	@es.MVMODE.PAGE = 0

	\BT.ON($BID+"BTN.GROUP", @GRP.NO)

	return[]
}

#=ES.MVMODE.BTN.GROUP.ON
{
	@GRP.NO  = @_PINT(1)
	@PAGE.NO = 1

	GOSUB[#=es.MVMODE.REF]

	return[]
}

//■[サムネイル]ボタン -----------------------------------------------
//
#=ES.MVMODE.BTN.THUMB.ON
{
	INT[@mLC=@_PINT(1)]

	GOSUB[#=es.MVMODE.CG.VIEW.CHK pint=(@PAGE.NO-1)*@THUMB.NUM+@mLC]
	IF[@_RINT(1)>0]
	{
		@CG.NO = (@PAGE.NO-1)*@THUMB.NUM+@mLC

	//\_dmes("SELNO="+$(@CG.NO))

		@es.STATE=101 //ムービー再生実行処理
	}
	IFEND[]

	return[]
}

//■[前のページ]ボタン -----------------------------------------------
//
#=ES.MVMODE.BTN.PREVPAGE.ON
{
	IF[@PAGE.NO>1]
	{
		@PAGE.NO-=1
		GOSUB[#=es.MVMODE.REF]
	}
	IFEND[]

	return[]
}

//■[次のページ]ボタン -----------------------------------------------
//
#=ES.MVMODE.BTN.NEXTPAGE.ON
{
	IF[@PAGE.NO < @PAGE.NUM]
	{
		@PAGE.NO+=1
		GOSUB[#=es.MVMODE.REF]
	}
	IFEND[]

	return[]
}

//■[戻る]ボタン -----------------------------------------------------
//
#=ES.MVMODE.BTN.BACK.ON
{
	@es.BGMMODE = 0

	\es.S.RETURN
	return[]
}


//■[戻る]ボタン -----------------------------------------------------
//
#=ES.MVMODE.BTN.VIEW.BACK.ON
{
	IF[@_OS_TIME(1) - @MOVIESTARTTIME > 800] //再生開始から800ms以上経過していたら
	{
		@es.STATE=103	//MOVIE再生終了
	}
	IFEND[]

	return[]
}



//////



//△ムービー鑑賞モード画像総数 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//
#=es.MVMODE.CG.MAX.GET
{
	return[rint=@CG.MAX]
}




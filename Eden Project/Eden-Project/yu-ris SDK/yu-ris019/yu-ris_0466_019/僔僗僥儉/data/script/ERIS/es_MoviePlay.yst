//=========================================================================
//
//■ERIS ムービー再生
//
//=========================================================================

//■スタティック変数宣言
{
	S_STR[$FN]
	S_INT[@SkipFlag]

}


//------------------------------------------------------------
// ムービー再生
//------------------------------------------------------------
#=es.MOVIEPLAY.LOOP
{
	//----------------------------------------------------------------
	// 
	//----------------------------------------------------------------
		//\BT.END()
		\WA(1)

	//----------------------------------------------------------------
	// ムービー鑑賞登録チェック
	//----------------------------------------------------------------
		GOSUB[#=es.MVMODE.CG.FLAG.SET pstr=$FN]

	//----------------------------------------------------------------
	//
	//----------------------------------------------------------------
	@es.MOVIEFLAG=1

	IF[1]

		//----------------------------------------------------------------
		// swfか否か ///x暫定処理
		//----------------------------------------------------------------
			INT[@swf]
			\_strright($FN, 4)
			//\_dmes($_RSTR(1))
			IF[$_RSTR(1)==".swf" || $_RSTR(1)==".SWF"] @swf=1 IFEND[]

		//----------------------------------------------------------------
		// ムービー再生
		//----------------------------------------------------------------
			IF[@swf==0]
			{
				INT[@codeccheck]
				MOVIEINFO[CODEC=$FN LET=@codeccheck]
				IF[@codeccheck>1]
				{
					IF[@_DEBUGMODE && @T(17,10)==1]
						\_dmes("ムービーファイル [ "+$FN+" ] が MPEG-1コーデックではありません。"+$_CHR(13)+$_CHR(13)+"MPEG-1コーデックでない場合、環境によって再生できない場合がありますので、"+$_CHR(13)+"MPEG-1コーデックのムービーを使用してください。", "エラー")
					IFEND[]
				}
				IFEND[]
				IF[@codeccheck==0]
				{
					IF[@_DEBUGMODE]
						\_dmes("ムービーファイル [ "+$FN+" ] が存在しません。", "エラー")
					IFEND[]

					IFBREAK[LV=3]
				}
				IFEND[]

				INT[@vol]
				\VOL.BGM.LS.GET(@vol)

				GOSUB[#=es.VOL.MASTER.GET];@vol=@vol*@_RINT(1)/256
				MOVIE[FILE=$FN]
				MOVIE[VOLUME=@vol] //BGM音量に合わせる
				WAIT[TIME=50]
				MOVIE[PLAY=1]
			}
			ELSE[]
			{
				FLASH[FILE=$FN PLAY=1]
			}
			IFEND[]

		//----------------------------------------------------------------
		// 再生開始時間を取得
		//----------------------------------------------------------------
			INT[@mTime=@_OS_TIME(1)]

		//----------------------------------------------------------------
		// クリック待ち or 再生終了待ち
		//----------------------------------------------------------------
			INT[@mRET]
			LOOP[]
			{
				IF[@swf==0]
				{
					MOVIEINFO[PLAY=1 LET=@mRET]
				}
				ELSE[]
				{
					FLASHINFO[PLAY=1 LET=@mRET]
				}
				IFEND[]

				IF[@mRET==0] LOOPBREAK[] IFEND[]

				IF[@SkipFlag==0]
				{
					IF[@_OS_TIME(1) - @mTime > 800]
					{
						IF[@_MOUSE_L==1 || @_KEY_ENTER==1 || @_KEY_ESC==1] LOOPBREAK[] IFEND[]
					}
					IFEND[]
				}
				IFEND[]

				WAIT[FRAME=1]
			}
			LOOPEND[]


		//----------------------------------------------------------------
		// ムービー再生終了
		//----------------------------------------------------------------
			IF[@swf==0]
			{
				MOVIEEND[]
			}
			ELSE[]
			{
				FLASHEND[]
			}
			IFEND[]

	IFEND[]

	@es.MOVIEFLAG=0

	//---------------------------------------------------------------
	// ログに空白行を出力する
	//---------------------------------------------------------------
	//	\LOG.NUM.PLUS
	//	\LOG.WRITE("　")

	//----------------------------------------------------------------
	// スクリプト位置を記憶する
	//----------------------------------------------------------------
		\SCRIPTPOS_SAVE

	//------------------------------------------------------------
	// 
	//------------------------------------------------------------
		@es.SCENARIOSTOP = 0

	//-------------------------------------------- ジャンプ
	//	\es.S.RETURN(999, 0)
		\es.S.RETURN(996, 0)

	return[]
}


//----------------------------------------------------------------
//■パラメータセット.
//----------------------------------------------------------------
#=es.MOVIEPLAY.SET
{
	$FN = $_PSTR(1)
	@SkipFlag = @_PINT(2)
	@es.SCENARIOSTOP = 2

	return[]
}


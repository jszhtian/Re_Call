//=========================================================================
//
//■ERIS ゲーム初期化
//
//=========================================================================

//■スタティック変数宣言
{

}

//====================================================================
//■LOOP
//====================================================================
#=ES.FIRST.LOOP
{

	//----------------------------------------------------------------
	// テキスト出力モードを手動に設定
	// (必ず各種定義ファイルを読みに行く前に実行すること)
	//----------------------------------------------------------------
		TEXT[OUTPUTMODE=0]


	//----------------------------------------------------------------
	// セーブデータ格納場所取得
	//----------------------------------------------------------------
		$es.SAVEPATH = $_SAVE_PATH								//YU-RISセーブデータパス


	//----------------------------------------------------------------
	// 各種定義ファイル
	//----------------------------------------------------------------
		GOSUB[#=ES.ADDVAR.DEFINE.PRE]		//追加変数定義(アップデート用)

		GOSUB[#=ES.FOLDER.DEFINE]			//(38)フォルダ定義

		GOSUB[#=ES.DEBUG.DEFINE]			//(37)デバッグ
		GOSUB[#=ES.SYSTEM.DEFINE]			//(17)システム
		GOSUB[#=ES.TITLE.DEFINE]			//(19)タイトル
		GOSUB[#=ES.GAMEMAIN.DEFINE]			//(36)メイン画面
		GOSUB[#=ES.SELECT.DEFINE]			//(22)選択肢
		GOSUB[#=ES.MENU.DEFINE]				//(20)メニュー
		GOSUB[#=ES.SAVELOAD.DEFINE]			//(11)セーブロード
		GOSUB[#=ES.CONFIG.DEFINE]			//(18)コンフィグ
		GOSUB[#=ES.LOG.DEFINE]				//(21)バックログ
		\_gosub(ES.DATE.DEFINE)				//(10)日付定義

		GOSUB[#=ES.CONF.DEFINE]				//(13)確認定義

		GOSUB[#=ES.BG.DEFINE]				//(23)
		GOSUB[#=ES.EV.DEFINE]				//(24)
		GOSUB[#=ES.ST.DEFINE]				//(28)
		GOSUB[#=ES.FACE.DEFINE]				//(15)
		GOSUB[#=ES.FONT.DEFINE]				//(--)35空き
		GOSUB[#=ES.BGM.DEFINE]				//(25)
		GOSUB[#=ES.SE.DEFINE]				//(26)
		GOSUB[#=ES.VOICE.DEFINE]			//(--)
		GOSUB[#=ES.VOLUME.DEFINE]			//(27)(12)(14)
		GOSUB[#=ES.TRANSITION.DEFINE]		//(29)
		GOSUB[#=ES.CHARNAME.DEFINE]			//(34)
		GOSUB[#=ES.CGCXY.DEF]				//(--)画像中心点定義

		GOSUB[#=ES.CGMODE.DEFINE]			//(30)ＣＧ鑑賞モード
		GOSUB[#=ES.CGMODE.LISTDEFINE]		//(--)ＣＧ鑑賞モードリスト
		GOSUB[#=ES.RPMODE.DEFINE]			//(31)シーン鑑賞モード
		GOSUB[#=ES.RPMODE.LISTDEFINE]		//(--)シーン鑑賞モードリスト
		GOSUB[#=ES.BGMMODE.DEFINE]			//(32)ＢＧＭ鑑賞モード
		GOSUB[#=ES.BGMMODE.LISTDEFINE]		//(--)ＢＧＭ鑑賞モードリスト
		GOSUB[#=ES.MVMODE.DEFINE]			//(33)ムービー鑑賞モード
		GOSUB[#=ES.MVMODE.LISTDEFINE]		//(--)ムービー鑑賞モードリスト

		GOSUB[#=ES.ADDVAR.DEFINE.POST]		//追加変数定義(アップデート用)

///xINJ
//		\_gosub(ES.ESCMODE.DEFINE)			//ＥＳＣ回避モード

	GOSUB[#=ES.LOG.DEFINE.INIT]
	GOSUB[#=es.Save.DEFINE.INIT]
	GOSUB[#=es.BT.TASK.INIT]

	//----------------------------------------------------------------
	// 起動チェック
	//----------------------------------------------------------------
		GOSUB[#=es.SYSTEMStartCheck]


///xS	デバッグ用にウインドウ位置ずらす
/*
	IF[@_DEBUGMODE==1]
		IF[@T(37,11)!=-1] WINDOW[NO=0 X=@T(37,11)] IFEND[]
		IF[@T(37,12)!=-1] WINDOW[NO=0 Y=@T(37,12)] IFEND[]
	IFEND[]
*/

	//----------------------------------------------------------------
	// ＦＰＳ設定
	//----------------------------------------------------------------
		FPS[SET=60 SKIP=1]

	//----------------------------------------------------------------
	// 入力設定
	//----------------------------------------------------------------
		INPUT[MOUSE=1 KEY=1 PAD=0]				// ユーザー入力許可設定
		MOUSE[MODE=1 TIME=2500 CLICK=0 WHEEL=0]	// マウス表示モード設定

	//----------------------------------------------------------------
	// マウスカーソル設定
	//----------------------------------------------------------------
	/*
		IF[$T(17,11)!=""]
			CG[ID=ES.MCURSOR Z=1 E=0 FILE=$T(17,11)]
			MOUSE[ID=ES.MCURSOR HX=@T(17,12) HY=@T(17,13)]
		IFEND[]
	*/

	//----------------------------------------------------------------
	// ウィンドウ情報取得
	//----------------------------------------------------------------
		WINDOWINFO[NO=0 SX=1 LET=@es.WSX]
		WINDOWINFO[NO=0 SY=1 LET=@es.WSY]

		WINDOWINFO[NO=0 CAPTION=1 LET=$es.WindowCaption1]
		GOSUB[#=es.CAPTION.REFRESH]

	//----------------------------------------------------------------
	// ゲームデータ初期化or読み込み
	//----------------------------------------------------------------
		\GSD.INIT
WINDOW[NO=0 E=1]

	//----------------------------------------------------------------
	// べき乗データ
	//----------------------------------------------------------------
		LOOP[SET=63] MATH[POW=1 SET=2 SET2=@_LC-1 LET=@es.POW2(@_LC-1)] LOOPEND[]
		LOOP[SET=20] MATH[POW=1 SET=10 SET2=@_LC-1 LET=@es.POW10(@_LC-1)] LOOPEND[]

	//----------------------------------------------------------------
	// 各レイヤー番号
	// ※特にいじる必要のない場合は変更しないでください
	//----------------------------------------------------------------
		@es.Z.BT = 99000000*@es.ZEX  // ボタン //これが一番上でないと、CGSは上手く働かない.
		@es.Z.SS = 95000000*@es.ZEX  // スクリーンショットレイヤ
		@es.Z.SP =   100000          // スプライト

		@es.TXTASK.TX.E  = 1
		@es.TXTASK.SN.E  = 1

	//----------------------------------------------------------------
	// フォント情報取得
	//----------------------------------------------------------------
		GOSUB[#=es.FONT.INIT]

	//----------------------------------------------------------------
	// ウィンドウ手動終了モード
	//----------------------------------------------------------------
		WINDOW[NO=0 CLOSEMODE=0]

	//----------------------------------------------------------------
	// デバッグ機能
	//----------------------------------------------------------------
		GOSUB[#=es.DBG.INIT]

	//----------------------------------------------------------------
	// 演出タスク
	//----------------------------------------------------------------
		GOSUB[#=es.EF.TASK]

	//----------------------------------------------------------------
	// ウェイト(タスクを一度動かしておく)
	//----------------------------------------------------------------
		WAIT[FRAME=1]

	//----------------------------------------------------------------
	// ウィンドウをアクティブにする
	//----------------------------------------------------------------
		WINDOW[NO=0 ACTIVE=1]

	//----------------------------------------------------------------
	// サウンド設定
	//----------------------------------------------------------------
		GOSUB[#=es.SOUND.INIT]

	//----------------------------------------------------------------
	// スクリーン設定
	//----------------------------------------------------------------
		IF[@es.GSD(141,01)==1] //初回起動なら
		{
			@es.GSD(141,02) = @T(17,01)
			IF[@es.GSD(141,02)==2 || @es.GSD(141,02)==3] @es.GSD(141,02)-=2 IFEND[] //以前のバージョンとの互換性の為.
		}
		ELSE[] //２回目以降の起動なら
		{
			IF[@_WINDOW_INIT_POS==1] @es.GSD(141,02) = 0 IFEND[] //「常に画面中央、通常サイズ」なら
		}
		IFEND[]

		GOSUB[#=ES.SCREENCHANGE_EXEC PINT=@es.GSD(141,02)]

		IF[@es.GSD(141,02)==1] WAIT[TIME=1000] IFEND[]


	//----------------------------------------------------------------
	// システム起動回数カウント
	//----------------------------------------------------------------
		@es.GSD(141,01) += 1 //システム起動回数

	//----------------------------------------------------------------
	// 既読率計算
	//----------------------------------------------------------------
		GOSUB[#=es.TX.KIDOKU.PER.GET]
	//	INT[@mPer=@_RINT(1)]
	//	\_dmes($(@mPer)+"％")

	//----------------------------------------------------------------
	// 音量反映
	//----------------------------------------------------------------
		\GSD.REFLECT

	//----------------------------------------------------------------
	//
	//----------------------------------------------------------------
		GOSUB[#=es.Initialize]

	//----------------------------------------------------------------
	// スクリーンショット
	//----------------------------------------------------------------
		\es.CGTSS
		\es.CGSSS

	//----------------------------------------------------------------
	// スクリーンショットを消去
	//----------------------------------------------------------------
		\es.CGSDELALL
		\es.CGTDEL

	//----------------------------------------------------------------
	// 
	//----------------------------------------------------------------
		IF[@_DEBUGMODE]
		{
			STR[$log="/"+"/※ｽｸﾘﾌﾟﾄﾌｧｲﾙ名が正しく取得できなかったり、行数が若干前の行を示している場合が"+$_CHR(13)+"/"+"/  あります。目安程度としてご了承ください。あと秀丸TagJumpに対応してます"+$_CHR(13)+"/"+"/"+"★=CG欠番, ◆=サウンド欠番"+$_CHR(13)+"/"+"/"]
			///xCOM:51 PSTR=$log
		}
		IFEND[]

	//----------------------------------------------------------------
	// サブタスク起動
	//----------------------------------------------------------------
	//	\_task(es.IDSubTask, ES.SCENARIO.SubLoop, 001)
		\_task(es.IDSubTask, ES.SCENARIO.SubLoop, 100000000)
	//	\_task(es.IDSubTask2, ES.SCENARIO.SubLoop2, @es.64BITMAX)
		\_task(es.IDSubTask2, ES.SCENARIO.SubLoop2, 1)


///x↓SCENEGAMEMAIN.INIT のやつをこちらへ引越

		//----------------------------------------------------------------
		// 
		//----------------------------------------------------------------
			\SUPERAUTOMODE_OFF

		//----------------------------------------------------------------
		// ゲームメインへ
		//----------------------------------------------------------------
	//		\es.S.GOSUB("ES.GAMEMAIN", 0, 0)
			\es.S.GO("ES.GAMEMAIN", 0, 0)

	return[]
}



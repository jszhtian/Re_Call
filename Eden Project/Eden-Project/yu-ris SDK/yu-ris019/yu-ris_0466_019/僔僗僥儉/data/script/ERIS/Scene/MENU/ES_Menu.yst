//============================================================================
//
//■ERIS メニュー
//
//============================================================================

//■スタティック変数宣言
{
}

//--------------------------------------------------------------------
//■INIT
//--------------------------------------------------------------------
#=ES.MENU.INIT
{
	IF[@es.STATE==0] //来た
	{
		\BT.W0.ALL("ES.MENU") //ボタンまだ無効

		IF[@es.RPMODE==0] //回想モードでなければ
//			\_dmes(saveします)
			\SAVE(50001, 3) //0=save,1=qsave,2=prevselsave
		IFEND[]

		\_gosub(ES.MENU.VoicePlay.1) //ボイス再生

		\SS.FADE(@T(20,01))

		@es.STATE=100
	}
	IFEND[]

	return[]
}


//--------------------------------------------------------------------
//■LOOP
//--------------------------------------------------------------------
#=ES.MENU.LOOP
{
	IF[@es.STATE==1] //戻った
	{
//		\GSD.SAVE ///x一応保存しておく
		IF[@es.STATE2==1]
		//	\SS.FADE(0)
			\es.CGTDEL
			GOSUB[#=es.BT.DRAW]
		ELSE[]
			\SS.FADE(@T(20,01))
		IFEND[]

		@es.STATE=100
	}
	IFEND[]

	IF[@es.STATE==100] //ボタン有効にする
	{
		\BT.W1.ALL("ES.MENU") //ボタン有効化
		@es.STATE = 101
	}
	IFEND[]

	IF[@es.STATE==101] //ループ
	{
	}
	IFEND[]

	//-------------------------------------------------------
	//■タイトル確認から戻った
	//-------------------------------------------------------
	IF[@es.STATE==901]
	{
		@es.STATE=001 //CANCEL
	}
	IFEND[]
	//-------------------------------------------------------
	//■タイトルに戻る
	//-------------------------------------------------------
	IF[@es.STATE==900]
	{
		//@es.STATE=998

		GOSUB[#=GO.TITLE.R2]

		\es.CGTDEL

		@es.SCENARIOSTOP = 0

		\es.S.RETURN()

	}
	IFEND[]

	return[]
}


//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//■ボタン処理
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

//■[LOG]ボタン ------------------------------------------------------
//
#=ES.MENU.BTN.LOG.ON
{
	\es.S.GOSUB("ES.LOG", 0, 0, 1, 0); return[]
}

//■[SAVE]ボタン -----------------------------------------------------
//
#=ES.MENU.BTN.SAVE.ON
{
//	IF[@L(8111)==0]
		\es.S.GOSUB("ES.SAVE", 0, 0, 1, 0)
//	IFEND[]
	return[]
}

//■[LOAD]ボタン -----------------------------------------------------
//
#=ES.MENU.BTN.LOAD.ON
{
	\es.S.GOSUB("ES.LOAD", 0, 0, 1, 0); return[]
}

//■[QSAVE]ボタン ----------------------------------------------------
//
#=ES.MENU.BTN.QSAVE.ON
{
	\es.S.GOSUB.OVER("ES.QSAVE", 0, 0, 1, 0); return[]
}

//■[QLOAD]ボタン ----------------------------------------------------
//
#=ES.MENU.BTN.QLOAD.ON
{
//ロードするファイル名取得
\_inttostr(@es.sdqsaveno, @es.sdketa)
STR[$Name="save"+$_RSTR(1)+".sd"]

//クイックセーブデータが有効か
GOSUB[#=es.SaveDataCheck pstr=$Name]
IF[@_RINT(1)]
{
	\es.S.GOSUB.OVER("ES.QLOADCONF", 0, @es.sdqsaveno, 1, 0)
}
IFEND[]

return[]
}

//■[PREVSEL]ボタン --------------------------------------------------
//
#=ES.MENU.BTN.PREVSEL.ON
{
	\es.S.GOSUB.OVER("ES.PREVSELCONF", 0, 0, 1, 0); return[]
}

//■[CONFIG]ボタン ---------------------------------------------------
//
#=ES.MENU.BTN.CONFIG.ON
{
	\_gosub(ES.CONFIG.VoicePlay.1)	///x時間がかかるのでここでボイス再生
	\es.S.GOSUB("ES.CONFIG", 10, 0, 1, 0); return[]
}

//■[ERASE]ボタン ----------------------------------------------------
//
#=ES.MENU.BTN.ERASE.ON
{
	\BT.ES20.ALL("ES.MENU");GOSUB[#=es.BT.DRAW]
	\es.S.GOSUB("ES.WINDOWERASE", 0, 0, 1, 1) //第4引数=瞬時に戻る
	return[]
}

//■[TITLE]ボタン ----------------------------------------------------
//
#=ES.MENU.BTN.TITLE.ON
{
	IF[@_PINT(1)==0] //タイトル確認
		\es.S.GOSUB.OVER("ES.TITLECONF", 0, 0, 901, 0)
	IFEND[]
	IF[@_PINT(1)==1] //タイトル確認(DIALOG)
		GOSUB[#=ES.TITLECONF.DIALOG]
		IF[@_RINT(1)==1]
			GOSUB[#=es.CGTSS.2]
			\es.S.RETURN.LBL("ES.GAMEMAIN", 900, 0)
		IFEND[]
	IFEND[]
	return[]
}

//■[RPMODE]ボタン ---------------------------------------------------
//
#=ES.MENU.BTN.RPMODE.ON
{
	IF[@_PINT(1)==0] //シーン鑑賞確認
		\es.S.GOSUB.OVER("ES.RPMODECONF", 0, 0, 1, 0); return[]
	IFEND[]
	IF[@_PINT(1)==1] //シーン鑑賞確認(DIALOG)

	IFEND[]
}

//■[END]ボタン ------------------------------------------------------
//
#=ES.MENU.BTN.END.ON
{
	IF[@_PINT(1)==0] //終了確認
		\es.S.GOSUB.OVER("ES.ENDCONF", 0, 0, 1, 0)
	IFEND[]
	IF[@_PINT(1)==1] //終了確認(DIALOG)
		GOSUB[#=ES.ENDCONF.DIALOG]
	IFEND[]
	return[]
}

//■[SCREEN]ボタン ---------------------------------------------------
//
#=ES.MENU.BTN.SCREEN.ON
{
	GOSUB[#=ES.SCREENCHANGE.LOOP]; return[]
}

//■[AUTO]ボタン -----------------------------------------------------
//
#=ES.MENU.BTN.AUTO.ON
{
	GOSUB[#=ES.AUTO.LOOP]
	\es.S.RETURN
	return[]
}

//■[SKIP]ボタン -----------------------------------------------------
//
#=ES.MENU.BTN.SKIP.ON
{
	GOSUB[#=ES.SKIP.LOOP]
	\es.S.RETURN
	return[]
}

//■[BACK]ボタン -----------------------------------------------------
//
#=ES.MENU.BTN.BACK.ON
{
	\es.S.RETURN; return[]
}



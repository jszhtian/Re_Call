//====================================================================
//
// マウスゲームサンプル
//
//====================================================================

#=MOUSEGAME_START


	//---------------------------------------------------------------
	// 変数の準備
	//---------------------------------------------------------------
		INT[@LC1] // ループカウンタ一時記憶用

		INT[@COL]       // 取得した色を格納しておくための変数
		INT[@GAMESTATE] // ゲームの状態を設定、取得するための変数

		INT[@CHECK_X(17)] // 自分が壁と当たったか否かを調べるための配列変数
		INT[@CHECK_Y(17)] // 自分が壁と当たったか否かを調べるための配列変数

		LOOP[SET=4]
			@LC1 = @_LC
			LOOP[SET=4]
				@CHECK_X((@LC1-1)*4+@_LC) = (@_LC-1)*2
				@CHECK_Y((@LC1-1)*4+@_LC) = (@LC1-1)*2
			LOOPEND[]
		LOOPEND[]


	#MOUSEGAME_READY

	//---------------------------------------------------------------
	// チップ画像の準備
	//---------------------------------------------------------------
		CG[NO= 1             A=064        FILE="cg/MOUSEGAME/cg"]   // 背景ＣＧ
		CG[NO= 5             A=064 MODE=3 FILE="cg/MOUSEGAME/stage"]   // ステージ

		CG[NO=10 X=010 Y=010 A=256        FILE="cg/MOUSEGAME/start"]   // スタート地点
		CG[NO=20 X=460 Y=302 A=  0        FILE="cg/MOUSEGAME/goal"]    // ゴール地点

		CG[NO=50 X=000 Y=000 A=  0        FILE="cg/MOUSEGAME/player"]  // 自分


	//---------------------------------------------------------------
	// 左上の青い領域にカーソルを合わせたらスタート.
	//---------------------------------------------------------------
		MOUSE[MODE=2]   // マウスカーソルを表示

		LOOP[]
			CGINFO[NO=10 LET=@COL ONMOUSE=1]
			IF[@COL!=-1] LOOPBREAK[] IFEND[]
			WAIT[FRAME=1]
		LOOPEND[]


	//---------------------------------------------------------------
	// スタート.
	//---------------------------------------------------------------

		CG[NO=10 A=  0] // スタート地点を消す
		CG[NO=20 A=256] // ゴール地点を表示
		CG[NO= 1 A=224] // 背景を明るくする
		CG[NO= 5 A=192] // ステージを明るくする

		MOUSE[MODE=0]   // マウスカーソルを消す


		LOOP[]

			CG[NO=50 X=@_MOUSE_X Y=@_MOUSE_Y A=256] // 自分を表示

			LOOP[SET=16]

				CGINFO[NO=5 LET=@COL ONMOUSE=1 SET=@_MOUSE_X+@CHECK_X(@_LC) SET2=@_MOUSE_Y+@CHECK_Y(@_LC)]
				IF[@COL==0xffffff]
					@GAMESTATE=1 // GAMEOVER
					LOOPBREAK[LV=2]
				IFEND[]

				CGINFO[NO=20 LET=@COL ONMOUSE=1 SET=@_MOUSE_X+@CHECK_X(@_LC) SET2=@_MOUSE_Y+@CHECK_Y(@_LC)]
				IF[@COL!=-1]
					@GAMESTATE=2 // GOAL
					LOOPBREAK[LV=2]
				IFEND[]

			LOOPEND[]

			WAIT[FRAME=1]

		LOOPEND[]


	//---------------------------------------------------------------
	// ゲームオーバー.
	//---------------------------------------------------------------
		IF[@GAMESTATE==1]

			LOOP[SET=32]
				CG[NO=5 Y+=20 X+=20] WAIT[FRAME=1]
				CG[NO=5 Y-=20 X-=20] WAIT[FRAME=1]
			LOOPEND[]

			//ゴールしたフラグを寝かせる(=0にする)
			@L.I(101)=0

			GO[#=MOUSEGAME_END]

		IFEND[]

	//---------------------------------------------------------------
	// ゴール.
	//---------------------------------------------------------------
		IF[@GAMESTATE==2]

			LOOP[SET=32]
				CG[NO=5 X+=5] WAIT[FRAME=1]
				CG[NO=5 X-=5] WAIT[FRAME=1]
			LOOPEND[]

			//ゴールしたフラグを立てる(=1にする)
			@L.I(101)=1

			GO[#=MOUSEGAME_END]

		IFEND[]



//---------------------------------------------------------------
// 終了
//---------------------------------------------------------------
#=MOUSEGAME_END



//最後は必ず作成したレイヤを全て消去してください。
//ERISに戻って以降、ゴミが表示されてしまうことがあります。
//音楽なども鳴らしている場合は SOUNDEND で必ず終了させてください。
//
CGEND[NO= 1]  // 背景ＣＧ
CGEND[NO= 5]  // ステージ
CGEND[NO=10]  // スタート地点
CGEND[NO=20]  // ゴール地点
CGEND[NO=50]  // 自分



//ERISに戻ります。再開先ラベル名を指定します。
//
\ERIS.ON(ミニゲーム_終了)



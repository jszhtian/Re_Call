//=========================================================================
//
// YU-RIS β３ 紹介
//
//
//=========================================================================
#SCENARIO_TITLESTART


\BG(bg_010)

どの紹介を見ますか？


\SEL.E(1,1,@G.I(1),1,1) //３つめの選択肢は @G.I(1) の値が 1 か 0 か によって出現するか否かが決まる.
\SEL.GO("INTRODUCTION", "INTRODUCTION2", "ROLLSAMPLE", "NOVELSAMPLE", "GOTOTITLE")
\SEL("基本機能紹介", "スプライト機能紹介", "スタッフロールサンプル", "ノベルサンプル", "タイトルへ戻る")


//-------------------------------------------------------------------------
//
// 基本機能紹介
//
//-------------------------------------------------------------------------
#INTRODUCTION

\DATE(7,21,金) // 日付をセット
\DATE.SHOW     // 日付ウィンドウを表示

\BG(BG_040) // 背景：通学路

\BGM(bgm01) // BGM再生

\枝理("P1>21") // 立ち絵をセット.(この後に「台詞」や「ウェイト」や「￥ＢＧ」「￥ＥＶ」が来た時に初めて立ち絵が表示されます)


枝理「こんにちはっ♪」 // セットされた立ち絵がここで表示されます


\枝理("P1>03")
枝理「はじめまして。\TC(0x00FFFF)≪YU-RIS／ユーリス≫β３\TCの機能を紹介させて頂きます枝理といいます♪」


\枝理("P1>23")
枝理「これからβ３でどんなことが出来るのか紹介していきますね♪」


\枝理("P1>03")
枝理「えとじゃあ、まず立ち絵表示機能から説明していきたいと思います」


\枝理("P1>01")
枝理「あそうそう、スクリプトファイルを追いながら動作を見ていくとより分かりやすいかもです♪」


\枝理("P1>03")
枝理「ちなみに、今動いているスクリプトファイルは\TC(0x00FFFF)『script/UserScript/イントロダクション.txt』\TCにありますよ」


\枝理("P1>23")
枝理「あ、そうそう、キーボードの\TC(0x00FFFF)『Ｆ１２』\TCキーを押してみてください」


\枝理("P1>21")
枝理「押すと新しいちっちゃなウィンドウがでますよね？」


\枝理("P1>01")
枝理「そこに、今動いているスクリプトのファイル名と行数が出てると思います」


\枝理("P1>23")
枝理「あと、よかったらキーボードの\TC(0x00FFFF)『Ｆ１１』\TCキーも押してみてください」


\枝理("P1>01")
枝理「いま実行されているスクリプトが開いて、あとカーソルも動いたと思います」


\枝理("P1>03")
枝理「Ｆ１１を押す度にカーソルが移動するので、いまどこが実行されているか、エディタで簡単に追うことができるんですよ♪」


\WA(300)




//-------------------------------------------------------------------------
//
// 立ち絵の説明
//
//-------------------------------------------------------------------------


\枝理("P1>01")
枝理「では、立ち絵命令の説明ですね」


\枝理("P1>05")
枝理「スクリプトファイルの今実行されている部分を見てみてください」


\枝理("P1>01") //←コレのことです。
枝理「私のセリフの直前に書いてある、\r\TC(0x00FFFF)『￥枝理（“Ｐ１＞０１”）』\TCっていうのが立ち絵命令です」


\枝理("P1>21")
枝理「￥マークのあとにある「枝理」っていう文字が私の名前で、つまり私が表示されるわけです」


\枝理("P1>01")
枝理「で、“Ｐ１＞０１”っていう文字が、ポーズや服装や表情を指定していて、この文字が変わると、私の表情とか服が変わったりするわけです」


\枝理("P1>05")
枝理「『Ｐ１』っていうのがポーズまたは服装画像で、『０１』っていうのが表情画像を指しています」


\枝理("P1>01")
枝理「『sample/cg/stand/eri』フォルダの中に同名の画像ファイルがあるんですけど、それをここで読み込んでいるわけですね」


\枝理("P1>05")
枝理「ちなみにこの書き方は最近できるようになった書き方で、従来通りの書き方もできるみたいです」


\枝理("P1>21")
枝理「もっと詳しい書き方は同梱のＥＲＩＳマニュアルに書かれてますのでそちらを見てみてください♪」


\枝理("P1>01")
枝理「えーとあとは、立つ位置の指定とか指定できて……」

\枝理("P1>21")
枝理「あ、じゃ、ちょっと動いてみますね」

\TS(L)
\枝理("P1>23",R)
枝理「ほっ」

\TS(L)
\枝理("P1>21",L)
枝理「ほっ」

\TS(LL)
\枝理("P1>21",M)
枝理「ほぉっ」


枝理「と、こんな感じで命令の第２パラメータっていうところに『Ｒ』とか『Ｌ』を指定することで、右左に表示できます」


\枝理("P1>03")
枝理「さらに細かい位置指定も出来て……」

\TS(S)
\枝理("P1>21",P13)
枝理「こそ…」

\TS(S)
\枝理("P1>21",P12)
枝理「こそこそ…」

\枝理("P1>01",P11)
枝理「と、『Ｒ』とか『Ｌ』の代わりに『Ｐ１』〜『Ｐ２７』っていう指定で細かい位置指定ができるんです」




// ★１
// 立ち絵位置自動調節機能
// このように２つ並べて書くと、自動的に枝理が左に、優理が右に表示されます
// 「￥枝理」命令が左に書いてあるので左に、「￥優理」命令が右に書いてあるので右、ということになります
// パっと見て分かりやすくなっているかと思います。
// ↓
\枝理("P1>07")\優理(a101)

優理「おはよう、枝理」



\枝理("P1>21")
枝理「おぉっ、お姉ちゃん、ちょうどいいところにっ！」


\優理(a007)
優理「……え？」


\枝理("P1>01")
枝理「今、お姉ちゃんが来たときのスクリプトを見てみてください。\TC(0x00FFFF)『★１』\TCっていうところです」


\枝理("P1>21")
枝理「立つ位置を自動的に左右に指定してくれる機能なので、利用すると便利ですよ♪」


\枝理("P1>23")
枝理「３人指定した場合は、自動的に『左』『真ん中』『右』に表示してくれます」


\優理(a407)
優理「…枝理……あの……そちらの方は？」


\枝理("P1>01")
枝理「『クリエイター』さんですっ」


\優理(b007)
優理「…クリエイター…さん…？」


\枝理("P1>21")
枝理「そう、『クリエイター』さんなのですっ」


\優理(b304)
優理「……あ、あの……はじめまして……木之原優理と言います……」


\枝理("P1>05")
枝理「お姉ちゃん緊張してる？」


\優理(b308)
優理「こ、こら……枝理…」


\枝理("P1>21")
枝理「あはは、それじゃあとは、立ち絵の重なり優先順位の指定ですね」


// 枝理の優先順位を１、優理の優先順位を２に設定
// ↓
\枝理("P1>26",M,1)\優理(a310,R,2)
枝理「お姉ちゃんはちょっとそのままでいてね」


\優理(a107,R)
優理「…え……うん……」


\枝理("P1>03")
枝理「いま、私がお姉ちゃんより前にいますよね」


枝理「ここで、優先順位を変更すると…」


// 枝理の優先順位を２、優理の優先順位を１に設定
// ↓
\枝理("P1>21",M,2)\優理(a107,R,1)
枝理「ささっ。このとおり♪　重なる順番が変わるんです」


\枝理("P1>01",L)
枝理「立ち絵に関してはこんな感じなんですけど、だいたい分かりました？」


\WA(300)


\優理(a103)
優理「あ、枝理……お姉ちゃん日直だから、もう行くね」


\枝理("P1>23")
枝理「あ、うん、分かったー。いってらっしゃーい」


// ★２
// パラメータを省略して名前だけ書くと立ち絵が消えます。
//
\優理


\WA(500)


\枝理("P1>21")
枝理「あ、またしてもナイスタイミング、お姉ちゃん♪」


\枝理("P1>01")
枝理「今みたいな感じで、パラメータを省略すると立ち絵が消えるようになってます。スクリプトの\r\TC(0x00FFFF)★２\TCのところです」


\枝理("P1>07")
枝理「んー……ひとまずそんなところかなー」



//-------------------------------------------------------------------------
//
// イベント絵
//
//-------------------------------------------------------------------------

\枝理("P1>21",M)
枝理「と、それじゃぁ次は背景やイベント絵の表示の仕方を説明しますね♪」


\枝理("P1>05")
枝理「命令は簡単、背景の読込が\TS(L)\TC(0x00FFFF)￥ＢＧ\TS\TC命令、\rイベント絵の読込が\TS(L)\TC(0x00FFFF)￥ＥＶ\TS\TC命令です」


\枝理("P1>05")
枝理「ちなみに、背景やイベント絵を読み込むと、立ち絵は自動的に消えるようになってるんですよ」


\枝理("P1>01")
枝理「やってみますね♪」


//★３
// 1000ミリ秒(１秒)かけてトランジション「11」で切り替えます。
// ※トランジション文字列に関しては「UserDefine/トランジション定義.txt」を参照してください。
//
\BG(BLACK,1000,11)


\WA(1000)


\BG(bg_040,1000,11)


\枝理("P1>23")
枝理「切り替わりました♪」


\枝理("P1>03")
枝理「いまトランジションで切り替わってましたよね。\TC(0x00FFFF)★３\TCのところで、トランジション『１１』を指定してます」


枝理「パラメータを省略すると、トランジションじゃなくてフェードになるんですよ」


\枝理("P1>21")
枝理「もっかいやってみますね。ついでに今度はゆっくりフェードしてみます」


\BG(bg_030,2000)


\WA(200)


\BG(bg_040,2000)


\枝理("P1>23")
枝理「こんな感じです♪」



\WA(500)


//-------------------------------------------------------------------------
//
// サウンド関係（ＢＧＭ、ＳＥ等）
//
//-------------------------------------------------------------------------

\枝理("P1>21")
枝理「じゃ次はサウンド関係の命令についての説明です♪」


\枝理("P1>03")
枝理「曲を指定するのは\TS(L)\TC(0x00FFFF)￥ＢＧＭ\TS\TC命令を使いますです」


\枝理("P1>23")
枝理「じゃあさっそく、今流れてる曲のを別の曲に変えてみますね♪」


// ★４
//
\BGM(bgm02)


\WA(3000,1) //スキップ禁止で２秒ウェイト


\枝理("P1>21")
枝理「変わりましたね♪」


\枝理("P1>01")
枝理「\TC(0x00FFFF)★４\TCのところで、もしその時点で他のＢＧＭが鳴っていた場合、自動的にクロスフェードで曲が切り替わる設定になっています」


\枝理("P1>03")
枝理「逆にＢＧＭが鳴っていなかった場合は、フェードじゃなくて最初から普通に曲が鳴る仕組みになってます」


/*
\枝理("P1>09")
枝理「あと効果音のほうは……」


\枝理("P1>61")
枝理「えっと、ごめんなさいっ、時間がないのでまた今度説明しますね♪」


//※効果音については仕様書に書いてありますので、ひとまずそちらを見てくださいませ
*/



//-------------------------------------------------------------------------
//
// 選択肢
//
//-------------------------------------------------------------------------

\枝理("P1>01")
枝理「次は選択肢について説明します。\r\TS(L)\TC(0x00FFFF)￥ＳＥＬ\TS\TC命令というのを使います」


\枝理("P1>21")
枝理「まずは通常の選択肢から出してみますね」



\SEL.GO(LABEL01_1, LABEL01_2, LABEL01_3)  //選択肢の行き先をセット.
\SEL(選択肢１,選択肢２,選択肢３)          //選択肢を出す



#=LABEL01_1

\枝理("P1>03")
枝理「\TS(LL)１番\TSを選択しました♪」

\GO(LABEL01_END)



#=LABEL01_2

\枝理("P1>03")
枝理「\TS(LL)２番\TSを選択しました♪」

\GO(LABEL01_END)



#=LABEL01_3

\枝理("P1>03")
枝理「\TS(LL)３番\TSを選択しました♪」

\GO(LABEL01_END)





#=LABEL01_END



\枝理("P1>01")
枝理「それじゃあ次はですね、『選択肢のランダム表示』をやってみますね♪」


\SEL.RND(1)                                          //選択肢の表示される順番をランダムにする
\SEL.GO(LABEL02_1, LABEL02_2, LABEL02_3, LABEL02_4)  //選択肢の行き先をセット.
\SEL(選択肢Ａ,選択肢Ｂ,選択肢Ｃ,選択肢Ｄ)            //選択肢を出す




#=LABEL02_1

\枝理("P1>03")
枝理「\TS(LL)Ａ\TSを選択しました♪」

\GO(LABEL02_END)



#=LABEL02_2

\枝理("P1>03")
枝理「\TS(LL)Ｂ\TSを選択しました♪」

\GO(LABEL02_END)



#=LABEL02_3

\枝理("P1>03")
枝理「\TS(LL)Ｃ\TSを選択しました♪」

\GO(LABEL02_END)



#=LABEL02_4

\枝理("P1>03")
枝理「\TS(LL)Ｄ\TSを選択しました♪」

\GO(LABEL02_END)




#=LABEL02_END


\枝理("P1>01")
枝理「ランダムに表示されました？」


\枝理("P1>61")
枝理「たまに順番通りに揃っちゃう場合もあるんですけどね。まぁランダムですからねー」


\枝理("P1>03")
枝理「えーとあとは……」


\枝理("P1>21")
枝理「あれですね。『制限時間付き選択肢』♪」


\枝理("P1>23")
枝理「次の例では、５秒以内に選択肢を選ばなかった場合に、ジャンプしないでそのまま次の命令が実行されます」



#=LABEL03  //あとでここに戻ってくるためのラベル.


\枝理("P1>21")
枝理「ではいきますよ〜。５秒ですからね」


\SEL.TIME(5000)                           //選択肢の制限時間を5000ミリ秒(＝5秒)にセット
\SEL.GO(LABEL03_1, LABEL03_2, LABEL03_3)  //選択肢の行き先をセット.
\SEL(選択肢■,選択肢●,選択肢★)          //選択肢を出す

//※何も選択しなかった場合、ラベルジャンプしないでそのまま次の命令へ
// ↓



\枝理("P1>03")
枝理「何も選択しませんでした♪」

\GO(LABEL03_END)



#=LABEL03_1

\枝理("P1>03")
枝理「\TS(LL)■\TSを選択しました♪」

\GO(LABEL03_END)



#=LABEL03_2

\枝理("P1>03")
枝理「\TS(LL)●\TSを選択しました♪」

\GO(LABEL03_END)



#=LABEL03_3

\枝理("P1>03")
枝理「\TS(LL)★\TSを選択しました♪」

\GO(LABEL03_END)





#=LABEL03_END

\枝理("P1>23")
枝理「今の、もう一回やってみます？」


#=LABEL04

\SEL.TIME(5000)              //選択肢の制限時間を5000ミリ秒(＝5秒)にセット
\SEL.GO(LABEL03, LABEL04_2)  //選択肢の行き先をセット.
\SEL(もう一度やる,やらない)  //選択肢を出す


\枝理("P1>49")
枝理「もー、やるんですかぁ？　やらないんですかぁ？」

\GO(LABEL04)



#=LABEL04_2

\枝理("P1>03")
枝理「じゃあ選択肢の説明はこれで終わりにしますね♪」




//-------------------------------------------------------------------------
//
// エンディング
//
//-------------------------------------------------------------------------

\BG(BLACK,800,13)

\WA(500)

\BG(bg_040,800,13)


\枝理("P1>03")
枝理「機能の説明は今日のところはこれでおしまいです」


\枝理("P1>07")
枝理「他にスプライト機能の説明もありますので、そちらも合わせて見てみてください♪」


\枝理("P1>05")
枝理「実際に触ってみて、もし使いやすいって感じてくれたら…」


\枝理("P1>26")
枝理「ぜひぜひ、YU-RISでノベル作りに挑戦してみてくださいね♪」


@G.I(1234)=1   //クリアフラグを立てる.(タイトル設計.txtファイル内のボタン定義でこの値をチェックしています)

\RP.SET(RP_01) //回想シーン「RP_01」のフラグをONにする.
\RP.SET(RP_02) //回想シーン「RP_02」のフラグをONにする.


\枝理("P1>21")
枝理「あ、そうそう、最後まで来たので、クリアフラグが立って、タイトル画面に項目が追加されました♪」


\枝理("P1>26")
枝理「回想シーンとか音楽モードが選べるようになりましたですよ♪」


\TS(L)
\枝理("P1>21")
枝理「ではでは、さよーなら♪」



\BGM(, 3000) // ３秒かけてＢＧＭフェードアウト


\BG(BLACK, 1500) // 1.5秒かけてＣＧフェードアウト


\WA(2000)


\DATE.HIDE  //日付ウィンドウ消す


\GO(SCENARIO_TITLESTART)


